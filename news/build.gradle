apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion android_compileSdkVersion.toInteger()
    buildToolsVersion android_buildToolsVersion

    defaultConfig {
        applicationId "com.news"
        minSdkVersion android_minSdkVersion
        targetSdkVersion android_targetSdkVersion
        /**
         * We need to read BUILD_NUMBER from environment variable as jenkins puts it here.
         * If you are not using jenkins then you want to override this line.
         */
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 9999
        versionName "1.0"
        resConfigs "en" // We're only supporting the English locale, don't add the others to the APK
        project.ext.set("archivesBaseName", "NAPP-" + versionName + "-" + versionCode);
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest {
            java {
                srcDir '../infra/src/test/java'
            }
        }

        test {
            java {
                srcDir '../infra/src/test/java'
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds true
        abortOnError true
        //lintConfig file("${project.rootDir}/config/quality/lint/lint.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("${project.buildDir}/reports/lint/lint.html")
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'  // butterknife
        exclude 'META-INF/LICENSE.txt' // commons-lang3-3.4.jar, javax.annotation-api-1.2jar
    }
}

ext {
    /* =========== Library versions =========== */

    /* Keep all support library versions consistent.
     * Find the changelog & latest version at https://developer.android.com/topic/libraries/support-library/revisions.html
     * IF we need to update version then we need to update support library on jenkins also.
     * Please fire "sudo ./android update sdk --no-ui --filter extra" @ "/var/lib/jenkins/.android-sdk/tools" path
     */
    supportLibVersion = '23.4.0'

    espressoLibVersion = '2.2.2'

    /* This is the version from the transitive dependency of retrofit2 (infra module).
     * Make sure to update this one whenever updating the version of retrofit2.
     */
    okHttpLibVersion = '3.2.0'

    /* Test library versions */
    commonsIoLibVersion = '2.4'
    junitLibVersion = '4.12';

    /* =========== END OF Library versions =========== */
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':infra')

    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:palette-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"

    compile 'com.github.bumptech.glide:glide:3.7.0'

    provided "org.projectlombok:lombok:1.16.8"

    testCompile "junit:junit:${junitLibVersion}"
    testCompile 'org.json:json:20160212'
    testCompile "commons-io:commons-io:${commonsIoLibVersion}"
    testCompile "com.squareup.okhttp3:mockwebserver:${okHttpLibVersion}"

    androidTestCompile "junit:junit:${junitLibVersion}"
    androidTestCompile "commons-io:commons-io:${commonsIoLibVersion}"
    androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoLibVersion}"
    androidTestCompile "org.mockito:mockito-core:1.9.5"
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
}
